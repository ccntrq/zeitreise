#!/usr/bin/env perl
use v5.20;
use feature 'signatures';

use strict;
use warnings;
no warnings 'experimental::signatures';

use FindBin;
use IPC::Open3;
use Symbol;
use IO::Select;

my ($onto) = @ARGV;
zeitreise($onto);

exit 1;

sub zeitreise($onto) {

    die "need 'onto'" unless length $onto;

    # set editor.
    # this editor will mark all commits for 'edit'
    my $oldEditor =
      setEnv( 'GIT_SEQUENCE_EDITOR', $FindBin::Bin . '/zeitreise_editor' );
    my $out = gitExec( 'rebase', '-i', $onto );

    modifyLoop();

    setEnv( 'GIT_SEQUENCE_EDITOR', $oldEditor );

    return 1;
}

sub modifyLoop {
    my $status = checkStatus();
    return if $status->{status} ne 'rebasing';

    modifyCommit();
    sleep 1;    # to get a different timestamp for each commit

    my $out = gitExec( 'rebase', '--continue' );

    modifyLoop();
}

sub modifyCommit {

    my $date = `date`;
    chomp $date;

    my $old_date = setEnv( 'GIT_COMMITTER_DATE', $date );

    my $out =
      gitExec( 'commit', '--amend', '--date', "'$date'", '-a', '--no-edit' );

    setEnv( 'GIT_COMMITTER_DATE', $old_date );
}

sub setEnv ( $key, $value ) {
    my $old = $ENV{$key};
    $ENV{$key} = $value;
    return $old;
}

sub getCommitInfo($onto) {

    # this will probably break gits '--root' option for now
    # should be easily fixable later on
    my $out = gitExec( 'log', '--pretty=fuller', "$onto..HEAD" );

    return logParser($out);
}

sub logParser($log) {

    # we need
    # - commitId (commit)
    # - AuthorDate
    # - CommitDate
    # - position

    my @out = reverse( split( "\n", $log ) );

    my @commitInfo;
    while ( scalar @out ) {
        my @munched = munch_message_and_whitespace(@out);
        my ( $info, @rest ) = extractCommitInfo(@munched);

        push @commitInfo, $info;
        @out = @rest;
    }

    return \@commitInfo;
}

sub extractCommitInfo(@lines) {
    return {} if !scalar @lines;

    my $res = {};
    $res->{CommitDate} = $lines[0];
    $res->{AuthorDate} = $lines[2];
    $res->{CommitID}   = $lines[4];

    # remove 'names' from the values
    $res->{$_} =~ s/^\S*\s// for keys %{$res};

    @lines = @lines[ 5 .. $#lines ];
    return ( $res, @lines );
}

sub munch_message_and_whitespace (@lines) {
    return () if !scalar @lines;

    my ( $line, @rest ) = @lines;
    if ( !length $line || $line =~ /^\s+/ ) {
        return munch_message_and_whitespace(@rest);
    }

    return ( $line, @rest );
}

sub checkStatus {

    my $out = gitExec('status');

    return statusParser($out);
}

sub statusParser($status) {
    my @status = split( "\n", $status );

    my $line = $status[0];

    # not in a rebase
    if ( $line =~ /^On branch (.*)$/ ) {
        return {
            status => 'normal',
            data   => {
                branch => $1,
            },
        };
    }

    # interactive rebase
    elsif ( $line =~ /^interactive rebase in progress; onto (.*)$/ ) {
        return {
            status => 'rebasing',
            data   => {
                onto => $1,
            },
        };

    }
    else {
        die "line: $line\nI'm confused. cannot parse 'status' output: $status";
    }
}

sub gitExec ( $cmd, @args ) {
    my @supportedCmds = qw(status rebase commit log);

    die "command '$cmd' is not supported. supported are: '"
      . join( ', ', @supportedCmds ) . "'!"
      if !grep /$cmd/, @supportedCmds;

    my $fullCmd = "git $cmd " . ( scalar @args ? join( ' ', @args ) : '' );

    my ( $inH, $outH, $errH );
    $errH = gensym();

    my $gitPid = open3( $inH, $outH, $errH, $fullCmd )
      or die "failed to execute: $!";

    my $sel = IO::Select->new();
    $sel->add( $outH, $errH );

    my ( $out, $err );
    while ( my @ready = $sel->can_read ) {
        for my $fh (@ready) {
            my $line = <$fh>;
            if ( !defined $line ) {    # EOF
                $sel->remove($fh);
                next;
            }
            if ( $fh == $outH ) {
                $out .= $line;
            }
            elsif ( $fh == $errH ) {
                $err .= $line;
            }
            else {
                die "How did we get here?";
            }
        }
    }

    waitpid( $gitPid, 0 );
    my $exit_status = $? >> 8;
    if ( $exit_status != 0 ) {
        say STDERR "ExitStatus: $exit_status";
        say STDERR "GitERR: $err" if $err;
        say STDERR "GitOUT: $out" if $out;
        die "FUCK";
    }

    return $out;
}
